# start
cmake_minimum_required (VERSION 2.6)
project (DashboardWatch)

#version
SET(DASHBOARDWATCH_MAJOR_VERSION 1)
SET(DASHBOARDWATCH_MINOR_VERSION 3)
SET(DASHBOARDWATCH_PATCH_VERSION 0)

# config
CONFIGURE_FILE(${CMAKE_CURRENT_SOURCE_DIR}/DashboardWatchConfig.h.in ${CMAKE_CURRENT_BINARY_DIR}/DashboardWatchConfig.h)

# qt
FIND_PACKAGE( Qt4 REQUIRED )
SET(QT_USE_QTNETWORK TRUE)
INCLUDE(${QT_USE_FILE})
ADD_DEFINITIONS(${QT_DEFINITIONS})

# sources
SET(DashboardWatch_SOURCES main.cpp DashboardWatch.cpp DashboardWatchLogger.cpp)
SET(DashboardWatch_HEADERS DashboardWatch.h DashboardWatchLogger.h)
SET(DashboardWatch_QOBJECT_HEADERS DashboardWatch.h)
SET(DashboardWatch_RCCS resources.qrc)

# generate rules for building source files from the resources
QT4_ADD_RESOURCES(DashboardWatch_RCC_SRCS ${DashboardWatch_RCCS})
QT4_WRAP_CPP(DashboardWatch_HEADERS_MOC ${DashboardWatch_QOBJECT_HEADERS})

# include dir
INCLUDE_DIRECTORIES(${CMAKE_CURRENT_BINARY_DIR})

# executable and link libraries
ADD_EXECUTABLE( ${PROJECT_NAME} WIN32 MACOSX_BUNDLE
    ${DashboardWatch_HEADERS}
    ${DashboardWatch_QOBJECT_HEADERS}
    ${DashboardWatch_SOURCES}
    ${DashboardWatch_HEADERS_MOC}
    ${DashboardWatch_RCC_SRCS})

IF(MSVC)
Set_Target_Properties(${PROJECT_NAME} PROPERTIES
  LINK_FLAGS_RELEASE /ENTRY:"mainCRTStartup"
  LINK_FLAGS_DEBUG /SUBSYSTEM:CONSOLE)
ENDIF()

TARGET_LINK_LIBRARIES( ${PROJECT_NAME} ${QT_LIBRARIES})

# cpack
INSTALL(TARGETS ${PROJECT_NAME} DESTINATION bin)

SET(CPACK_PACKAGE_VERSION_MAJOR ${DASHBOARDWATCH_MAJOR_VERSION})
SET(CPACK_PACKAGE_VERSION_MINOR ${DASHBOARDWATCH_MINOR_VERSION})
SET(CPACK_PACKAGE_VERSION_PATCH ${DASHBOARDWATCH_PATCH_VERSION})
IF(WIN32)
  SET(QT_DLLS "${QT_BINARY_DIR}/QtCore4.dll" "${QT_BINARY_DIR}/QtGui4.dll" "${QT_BINARY_DIR}/QtNetwork4.dll")
  INSTALL(FILES ${QT_DLLS} DESTINATION bin)
  SET(CPACK_GENERATOR ZIP)
  SET(CPACK_SOURCE_GENERATOR ZIP)
ELSE(WIN32)
  SET(CPACK_GENERATOR TGZ)
  SET(CPACK_SOURCE_GENERATOR TGZ)
ENDIF(WIN32)

INCLUDE(CPack)
